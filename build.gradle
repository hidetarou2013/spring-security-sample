subprojects {
  apply plugin: 'war'
  
  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'
  [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

  repositories {
    mavenCentral()
  }

  dependencies {
    providedCompile 'fish.payara.extras:payara-micro:4.1.1.171.0.1'
    providedCompile 'javax:javaee-api:7.0'
    
    if (project.name != 'app') {
      compile project(':app')
    }
  }
}

task start(type: Exec) {
  def payaraJarPath = subprojects[0]
                          .configurations
                          .providedCompile
                          .find {it.name =~ /payara-micro.*\.jar/}
                          .absolutePath
  
  def nonSecureWarPath = project(':non-secure').war.archivePath
  def secureWarPath = project(':secure').war.archivePath
  commandLine(
    'java',
      '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=8000',
      '-jar', payaraJarPath,
      '--deploy', nonSecureWarPath,
      '--deploy', secureWarPath
  )
}

start.dependsOn ':non-secure:war', ':secure:war'
